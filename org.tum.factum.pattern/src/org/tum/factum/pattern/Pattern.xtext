grammar org.tum.factum.pattern.Pattern with org.eclipse.xtext.common.Terminals
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate pattern "http://www.tum.org/factum/pattern/Pattern"

Pattern returns Pattern:
	{Pattern} 
	'Pattern' name=ID 'ShortName' psname=ID
	'{'
		('DTSpec' '{' dtSpec+=DataTypeSpec( "," dtSpec+=DataTypeSpec)* '}' )?
		('CTypes' '{' componentTypes+=ComponentType	( "," componentTypes+=ComponentType)* '}' )?
 
	    ('ArchSpec' '{' 
	    	(ctaCmpVar+=ComponentVariable ("," ctaCmpVar+=ComponentVariable)*) 			//Declaration of Component Variables
	    	ctaDtVars=DTVariables 														//Declaration of DT Variables
	    	
	    	
	    	'}'
	    )?
	    
		('SubPattern' '{' SubPattern+=Pattern ( "," SubPattern+=Pattern)* '}' )?
	'}'
;
DTVariables:
	(dtVars+=DataTypeVariable ("," dtVars+=DataTypeVariable)*)
;
ComponentType returns ComponentType:
	{ComponentType} 
	'CType'	name=EString ('ShortName' ctsname=ID)? 
	'{'
	    ('InputPorts' '{' inputPorts+=InputPort ( "," inputPorts+=InputPort)* '}' )?
	    ('OutputPorts' '{' outputPorts+=OutputPort ( "," outputPorts+=OutputPort)* '}' )?
	    
	    ('bAssertions' '{' 
	    	(btaDtVar+=DataTypeVariable ("," btaDtVar+=DataTypeVariable)*)?
	    	
	    )?
	
	'}'
;
VariableStatus: 
	'rig' | 'flex'
;
ComponentVariable:
	VariableStatus	name=ID ':' cmptypAssigned=[ComponentType|ID]?
;
DataTypeVariable: 
	VariableStatus name=ID ':' varSortType=[Sort|QualifiedName]
;

OutputPort returns OutputPort:
	{OutputPort} 
	'OutputPort' name=EString '(' 
		('Type' ':' outputPrtSrtTyp=[Sort|QualifiedName]) 
		('connects' connects+=[InputPort|QualifiedName]*)?
	')'
;
InputPort returns InputPort:
	{InputPort}	
	'InputPort' name=ID '('
		('Type' ':' inputPrtSrtTyp=[Sort|QualifiedName])
	    ('connects' connects+=[OutputPort|QualifiedName]*)?
	')'
;
DataTypeSpec returns DataTypeSpec:
	{DataTypeSpec}	
	'DT' name =ID	'('
		'Sort' (dtSorts+=Sort ("," dtSorts+=Sort)*)?
		('Operation' (dtOps+=Operation ("," dtOps+=Operation)*))?
		('Predicate' (dtPredicate+=DTPredicate ("," dtPredicate+=DTPredicate)*))?
		(sortName=[Sort|ID] 'generatedBy' (returnOps+=[Operation|ID] ("," returnOps+=[Operation|ID])*))?		
	')'
;
DTPredicate:
	name=ID ':' (pInput+=[Sort|QualifiedName] ("," pInput+=[Sort|QualifiedName])*)?
;
Operation:
	name=ID ':'  (dtInput+=[Sort|QualifiedName] ("," dtInput+=[Sort|QualifiedName])*)? '=>' (dtOutput=[Sort|QualifiedName])?
;
Sort:
	name=ID
;


//Begin common rules for CTA, Architectural Guarantee(AG) and Behavior Trace Assertions (BTA)
PortRef:
	InputPort | OutputPort
;
CmpVariableRef:
	cmpRef=[ComponentVariable|ID] '.' portRef=[PortRef|ID]
;
Terms:
	DataTypeVariable 
;
Term: 
	terms=[Terms|ID] | CmpVariableRef | termOperatorFunction=TermOperatorFunction
;
TermOperatorFunction:
	trmOperator=[Operation|QualifiedName] "(" (trmOperands+=Term ("," trmOperands+=Term)*) ")" 
;

//End

// Begin Configuration Trace Assertions(CTA) - Architectural Configuration or Configuration Trace Assertion 
CtaRefComponentVariableInputPort:
	inptPrtCmpRef=[ComponentVariable|ID] '.' inputPrtrf=[InputPort|ID]
;
CtaRefComponentVariableOutputPort:
	outptPrtCmpRef=[ComponentVariable|ID] '.' outputPrtrf=[OutputPort|ID]
;
CtaPredicateTerms:
	ctaPTerm1=Term equalityOperator=EqualityElementOperator ctaPTerm2=Term
;
CtaPredicateConn: 
	ctaConn='conn' ('(' ctaConnCmpVarOutputPort=CtaRefComponentVariableOutputPort ','	ctaConnCmpVarInptPort=CtaRefComponentVariableInputPort ')')
;
CtaPredicateEq: 
	ctaConn='eq' ('(' componentVariable+=[ComponentVariable|ID] ','	componentVariable+=[ComponentVariable|ID] ')')
;
CtaPredicateVal:
	ctaVal='val' ('(' valCtaCmpVaref=CmpVariableRef ',' ctaValTerms=Term ')') 
;
CtaPredicate:
	cAct='cAct' ('('cActCmpVar=[ComponentVariable|ID] ')') |
	pAct='pAct' ('(' pActCtaCmpVaref=CmpVariableRef ')') 
;


// End CTA


btaExpression:
	BehaviorTraceAssertion
; 
BehaviorTraceAssertion returns btaExpression:
	UnaryOperator '(' asertion=btaFunction ')'
;
btaFunction returns btaExpression:
	btaExpression |
	BA
;
BA:
	inputPortA=[InputPort|QualifiedName] '=' outputPortA=[OutputPort|QualifiedName]
;
acExpression:
	ArchitectureConstraint
;
ArchitectureConstraint	returns acExpression:
	UnaryOperator '(' asertion=archConstFunction ')'
;
archConstFunction:
	acExpression |
	AC
;
AC:
	InputPort=[InputPort|QualifiedName] '=' OutputPort=[OutputPort|QualifiedName]
;

//Begin Operator rules for CTA, BTA, and AG ....
UnaryOperator: 
	ltlG='G' | ltlX='X' |	ltlF='F' | //Linear Temporal Logic Operators
	neg='¬'
;
BinaryOperator: 
	'⇒' | '∧' | '∨' | '⇔' | 'W' | 'U' //operates F and F, F is a formula 
;
LogicOperator:
	'¬' 
;
EqualityElementOperator: //Maybe not needed (check?)
	'='
;
UnaryOp:
	'¬' 
;
//End
QualifiedName:
    ID ('.' ID)*
;
EString returns ecore::EString:
	STRING | ID
;
	
