grammar org.tum.factum.pattern.Pattern with org.eclipse.xtext.common.Terminals
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate pattern "http://www.tum.org/factum/pattern/Pattern"

Pattern returns Pattern:
	{Pattern} 
	'Pattern' name=ID 'ShortName' psname=ID
	'{'
		('DTSpec' '{' dtSpec+=DataTypeSpec( "," dtSpec+=DataTypeSpec)* '}' )?
		('CTypes' '{' componentTypes+=ComponentType	( "," componentTypes+=ComponentType)* '}' )?
 
	    ('ArchSpec' '{' 
	    	(ctaCmpVar+=ComponentVariable ("," ctaCmpVar+=ComponentVariable)*) 			//Declaration of Component Variables
	    	ctaDtVars=DTVariables 														//Declaration of DT Variables
	    	
	    	
	    	'}'
	    )?
	    
		('SubPattern' '{' SubPattern+=Pattern ( "," SubPattern+=Pattern)* '}' )?
	'}'
;
DTVariables:
	(dtVars+=DataTypeVariable ("," dtVars+=DataTypeVariable)*)
;
ComponentType returns ComponentType:
	{ComponentType} 
	'CType'	name=EString ('ShortName' ctsname=ID)? 
	'{'
	    ('InputPorts' '{' inputPorts+=InputPort ( "," inputPorts+=InputPort)* '}' )?
	    ('OutputPorts' '{' outputPorts+=OutputPort ( "," outputPorts+=OutputPort)* '}' )?
	    
	    ('bAssertions' '{' 
	    	(btaDtVar+=DataTypeVariable ("," btaDtVar+=DataTypeVariable)*)?
	    	
	    )?
	
	'}'
;
VariableStatus: 
	'rig' | 'flex'
;
ComponentVariable:
	VariableStatus	name=ID ':' cmptypAssigned=[ComponentType|ID]?
;
DataTypeVariable: 
	VariableStatus name=ID ':' varSortType=[Sort|QualifiedName]
;

OutputPort returns OutputPort:
	{OutputPort} 
	'OutputPort' name=EString '(' 
		('Type' ':' outputPrtSrtTyp=[Sort|QualifiedName]) 
		('connects' connects+=[InputPort|QualifiedName]*)?
	')'
;
InputPort returns InputPort:
	{InputPort}	
	'InputPort' name=ID '('
		('Type' ':' inputPrtSrtTyp=[Sort|QualifiedName])
	    ('connects' connects+=[OutputPort|QualifiedName]*)?
	')'
;
DataTypeSpec returns DataTypeSpec:
	{DataTypeSpec}	
	'DT' name =ID	'('
		'Sort' (dtSorts+=Sort ("," dtSorts+=Sort)*)?
		('Operation' (dtOps+=Operation ("," dtOps+=Operation)*))?
		('Predicate' (dtPredicate+=DTPredicate ("," dtPredicate+=DTPredicate)*))?
		(sortName=[Sort|ID] 'generatedBy' (returnOps+=[Operation|ID] ("," returnOps+=[Operation|ID])*))?		
	')'
;
DTPredicate:
	name=ID ':' (pInput+=[Sort|QualifiedName] ("," pInput+=[Sort|QualifiedName])*)?
;
Operation:
	name=ID ':'  (dtInput+=[Sort|QualifiedName] ("," dtInput+=[Sort|QualifiedName])*)? '=>' (dtOutput=[Sort|QualifiedName])?
;
Sort:
	name=ID
;

// Begin Configuration Trace Assertions(CTA) - Architectural Configuration or Configuration Trace Assertion 
CtaPortRef:
	InputPort | OutputPort
;
CtaRefComponentVariable:
	cmpRef=[ComponentVariable|ID] '.' ctaPortRef=[CtaPortRef|ID]
;
CtaRefComponentVariableInputPort:
	inptPrtCmpRef=[ComponentVariable|ID] '.' inputPrtrf=[InputPort|ID]
;
CtaRefComponentVariableOutputPort:
	outptPrtCmpRef=[ComponentVariable|ID] '.' outputPrtrf=[OutputPort|ID]
;
CtaTerms:
	DataTypeVariable 
;
CtaTermOperatorFunction:
	ctaTrmOperator=[Operation|QualifiedName] "(" (ctaTrmOperands+=CtaTerm ("," ctaTrmOperands+=CtaTerm)*) ")" 
;
CtaTerm:
	ctaTrms=[CtaTerms|ID] | CtaRefComponentVariable | ctaTrmOperatorFunction=CtaTermOperatorFunction
;

// End CTA


btaExpression:
	BehaviorTraceAssertion
; 
BehaviorTraceAssertion returns btaExpression:
	LtlOp '(' asertion=btaFunction ')'
;
btaFunction returns btaExpression:
	btaExpression |
	BA
;
BA:
	inputPortA=[InputPort|QualifiedName] '=' outputPortA=[OutputPort|QualifiedName]
;
acExpression:
	ArchitectureConstraint
;
ArchitectureConstraint	returns acExpression:
	LtlOp '(' asertion=archConstFunction ')'
;
archConstFunction:
	acExpression |
	AC
;
AC:
	InputPort=[InputPort|QualifiedName] '=' OutputPort=[OutputPort|QualifiedName]
;
LtlOp:
	('G' | 'X' | 'F')
;
QualifiedName:
    ID ('.' ID)*
;
EString returns ecore::EString:
	STRING | ID
;
	
